plugins {
    id 'com.gradle.plugin-publish' version '0.9.3'
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'

group = "us.ihmc.gradle"
version = "0.0.20"

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

project.ext.licenseURL = "http://www.apache.org/licenses/LICENSE-2.0.txt"
project.ext.licenseName = "Apache License, Version 2.0"
project.ext.bintrayLicenseName = "Apache-2.0"

project.sourceSets {
    main {
        groovy {
            srcDirs = ['groovySrc']
        }

        resources {
            srcDirs = ['src', 'resources']
        }
    }

    test {
        groovy {
            srcDirs = ['groovyTest']
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.5'
    compile 'ca.cutterslade.gradle:gradle-dependency-analyze:1.0.3'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    from sourceSets.main.allGroovy
}

task docJar(type: Jar, dependsOn: groovydoc) {
    from groovydoc.destinationDir
}

jar {
    manifest {
        attributes(
                "Created-By": "IHMC Gradle Build Script",
                "Implementation-Title": project.name,
                "Implementation-Version": project.version,
                "Implementation-Vendor": "IHMC",

                "Bundle-Name": project.name,
                "Bundle-Version": project.version,
                "Bundle-License": "${project.ext.licenseURL}",
                "Bundle-Vendor": "IHMC")
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            group "${project.group}"
            artifactId "${project.name}"
            version "${project.version}"

            from components.java

            artifact sourceJar {
                classifier "sources"
            }

            artifact docJar {
                classifier "javadoc"
            }
        }
    }
}

pluginBundle {
    website = 'https://github.com/ihmcrobotics/ihmc-build'
    vcsUrl = 'https://github.com/ihmcrobotics/ihmc-build'
    tags = ['build', 'ihmc', 'robotics']
    description = "IHMC Robotics's Gradle common build logic plugin"

    plugins {
        ihmcBuildPlugin {
            id = 'us.ihmc.gradle.ihmc-build'
            displayName = 'IHMC Build Plugin'
            version = project.version
        }
    }
}
